plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.android.gms.oss-licenses-plugin'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace 'io.github.lee0701.gukhanwiki.android'
    compileSdk 33

    defaultConfig {
        applicationId "io.github.lee0701.gukhanwiki.android"
        minSdk 16
        targetSdk 33
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

    }

    flavorDimensions "server"
    productFlavors {
        development {
            dimension "server"
            buildConfigField 'String', 'API_PROTOCOL', "\"https\""
            buildConfigField 'String', 'API_HOST', "\"${properties.getProperty("api.host")}\""
            buildConfigField 'String', 'REST_BASE_PATH', "\"${properties.getProperty("api.rest-base-path")}\""
            buildConfigField 'String', 'ACTION_BASE_PATH', "\"${properties.getProperty("api.action-base-path")}\""
            buildConfigField 'String', 'DOC_PATH', "\"${properties.getProperty("api.doc-path")}\""
            manifestPlaceholders = [hostName: "${properties.getProperty("api.host")}"]
        }
        production {
            dimension "server"
            buildConfigField 'String', 'API_PROTOCOL', "\"https\""
            buildConfigField 'String', 'API_HOST', '"wiki.xn--9cs231j0ji.xn--p8s937b.net"'
            buildConfigField 'String', 'REST_BASE_PATH', "\"/rest.php/v1/\""
            buildConfigField 'String', 'ACTION_BASE_PATH', "\"/api.php/\""
            buildConfigField 'String', 'DOC_PATH', "\"/wiki/\""
            manifestPlaceholders = [hostName: "wiki.xn--9cs231j0ji.xn--p8s937b.net"]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:5.0.0-alpha.11'
    implementation 'org.jsoup:jsoup:1.15.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}